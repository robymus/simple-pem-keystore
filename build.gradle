plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
}

group = "io.r2"
version = "0.4-SNAPSHOT"

defaultTasks "jar"

repositories {
    mavenCentral()
}

dependencies {
    implementation (
        'com.fasterxml.jackson.core:jackson-databind:2.12.5'
    )
    testImplementation (
        'org.testng:testng:7.4.0',
        'org.assertj:assertj-core:3.20.2',
        'org.mockito:mockito-core:3.12.4'
    )
}

test {
    useTestNG()
}

javadoc {
    exclude "io/r2/simplepemkeystore/spi/**"
}

sourceCompatibility = 1.9
targetCompatibility = 1.9

java {
    withJavadocJar()
    withSourcesJar()
}

compileJava {
    options.deprecation = true;
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'Simple PEM KeyStore'
                description = 'A java keystore implementation to use PEM files directly.'
                url = 'https://github.com/robymus/simple-pem-keystore'

                scm {
                    connection = 'scm:git:https://github.com/robymus/simple-pem-keystore.git'
                    developerConnection = 'scm:git:git@github.com:robymus/simple-pem-keystore.git'
                    url = 'https://github.com/robymus/simple-pem-keystore'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'robymus'
                        name = 'Robert Elek'
                        email = 'r@r2.io'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }}

signing {
    sign publishing.publications.mavenJava
}
